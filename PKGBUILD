# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=irqbalance-git
pkgver=1.5.0.r5.g40c1060
pkgrel=1
pkgdesc="A daemon to help balance the CPU load generated by interrupts across all of a systems CPUs"
arch=('i686' 'x86_64')
url="https://irqbalance.github.io/irqbalance/"
license=('GPL')
depends=('glib2' 'libcap-ng' 'libsystemd' 'numactl')
makedepends=('git')
provides=('irqbalance')
conflicts=('irqbalance')
backup=('etc/irqbalance.env')
source=("git+https://github.com/Irqbalance/irqbalance.git")
sha256sums=('SKIP')


prepare() {
  cd "irqbalance"

  sed -i -e 's|/usr/sbin/|/usr/bin/|g' "misc/irqbalance.service"
  sed -i -e 's|EnvironmentFile=/path/to|EnvironmentFile=/etc|g' "misc/irqbalance.service"
}

pkgver() {
  cd "irqbalance"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "irqbalance"

  ./autogen.sh
  ./configure \
    --prefix="/usr" --sbindir="/usr/bin" \
    --with-systemd
  make
}

package() {
  cd "irqbalance"

  make DESTDIR="$pkgdir" install

  install -Dm644 "misc/irqbalance.env" "$pkgdir/etc/irqbalance.env"
  install -Dm644 "misc/irqbalance.service" "$pkgdir/usr/lib/systemd/system/irqbalance.service"
}
